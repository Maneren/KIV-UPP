builddir := justfile_directory() / "build"
export CMAKE_EXPORT_COMPILE_COMMANDS := "1"
export CXXFLAGS := "-march=native -flto=thin -O3"

[private]
default:
    @just --list

configure:
    mkdir -p {{ builddir }}
    cmake -DCMAKE_BUILD_TYPE=Debug -S . -B {{ builddir }} -G Ninja

configure_release:
    mkdir -p {{ builddir }}
    cmake -DCMAKE_BUILD_TYPE=RelWithDebInfo -S . -B {{ builddir }} -G Ninja

build:
    cmake --build {{ builddir }}

sample: build
    time {{ builddir }}/meteo --serial meteodata/stanice.csv meteodata/mereni.csv
    time {{ builddir }}/meteo --parallel meteodata/stanice.csv meteodata/mereni.csv

valgrind: build
    valgrind --leak-check=full {{ builddir }}/meteo --serial meteodata/stanice.csv meteodata/mereni.csv
    valgrind --leak-check=full {{ builddir }}/meteo --parallel meteodata/stanice.csv meteodata/mereni.csv

perf: build
    perf record -F 32000 --call-graph=dwarf -m 128 -o perf.serial.data {{ builddir }}/meteo --serial meteodata/stanice.csv meteodata/mereni.csv
    perf record -F 32000 --call-graph=dwarf -m 128 -o perf.parallel.data {{ builddir }}/meteo --parallel meteodata/stanice.csv meteodata/mereni.csv
    perf script -i perf.serial.data > perf.serial.script
    perf script -i perf.parallel.data > perf.parallel.script

clean:
    rm -rf build
